%{
// ignore_for_file: prefer_final_locals

import 'package:source_span/source_span.dart';

import '../grammar/grammar.dart';
import '../grammar/production_rule.dart';

}%

`Grammar`
Start =>
  S
  g = Globals?
  m = Members?
  r = ProductionRule+ !.
  $ = { $$ = Grammar(globals: g, members: m, rules: r); }

`String`
Globals =>
  '%{'
  $ = <
    @while (*) {
      ! "}%" .
    }
  >
  '}%' S

`String`
Members =>
  '%%'
  $ = <
    @while (*) {
      ! "%%" .
    }
  >
  '%%' S

`ProductionRule`
ProductionRule =>
  t = Type?
  i = Identifier
  a = ProductionRuleArguments?
  '=>' S
  e = Expression
  [;]? S
  $ = { $$ = ProductionRule(expression: e, expected: a, name: i, resultType: t ?? ''); }

`String`
ProductionRuleArguments =>
  '(' S
  $ = String
  ')' S

`Expression`
Expression('expression') =>
  OrderedChoice

`Expression`
OrderedChoice =>
  n = Sequence
  { final l = [n]; }
  @while (*) {
    ('/' / '-'*) S
    n = Sequence
    { l.add(n); }
  }
  $ = { $$ = OrderedChoiceExpression(expressions: l); }

`Expression`
Sequence =>
  n = Typing+
  b = ('~' S $:Block)?
  $ = {
    final e = SequenceExpression(expressions: n);
    $$ = b == null ? e : CatchExpression(expression: e, catchBlock: b);
  }

`Expression`
Typing =>
  t = Type?
  e = Assignment
  $ = { $$ = t == null ? e : TypingExpression(expression: e, type: t); }

`Expression`
Assignment =>
  v = (Identifier / $:'$' S)
  ('=' S / ':' S)
  e = Prefix
  $ = { $$ = VariableExpression(expression: e, name: v); }
  ----
  Prefix

`Expression`
Prefix =>
  p = ($:'!' S / $:'&' S)?
  $ = Suffix
  { switch(p) {
    case '!':
      if ($ is ActionExpression) {
        $ = PredicateExpression(code: $.code, negate: true);
      } else {
        $ = NotPredicateExpression(expression: $);
      }
      break;
    case '&':
    if ($ is ActionExpression) {
        $ = PredicateExpression(code: $.code, negate: false);
      } else {
        $ = AndPredicateExpression(expression: $);
      }
      break;
  }
}

`Expression`
Suffix =>
  $ = Primary
  (
    '*' S { $ = ZeroOrMoreExpression(expression: $); }
    ----
    '+' S { $ = OneOrMoreExpression(expression: $); }
    ----
    '?' S { $ = OptionalExpression(expression: $); }
  )?

`Expression`
Primary('expression') =>
  CharacterClass
  ----
  Literal
  ----
  Group
  ----
  Repeater
  ----
  Nonterminal
  ----
  Action
  ----
  AnyCharacter
  ----
  Match

`Expression`
Action =>
  b = Block
  $ = { $$ = ActionExpression(code: b); }

`Expression`
AnyCharacter =>
  '.' S
  $ = { $$ = AnyCharacterExpression(); }

`Expression`
CharacterClass =>
  { var negate = false; }
  (
    '[^' { negate = true; }
    ----
    '['
  )
  r = @while (+) {
    ! ']'
    $ = Range
  }
  ']' S
  $ = { $$ = CharacterClassExpression(ranges: r, negate: negate); }

`Expression`
Group =>
  '(' S
  $ = Expression
  ')' S
  { $ = GroupExpression(expression: $); }

`Expression`
Literal =>
  s = SQString
  $ = { $$ = LiteralExpression(literal: s); }
  ----
  s = DQString
  $ = { $$ = LiteralExpression(literal: s, silent: true); }

`Expression`
Match =>
  '<' S
  e = Expression
  '>' S
  $ = { $$ = MatchExpression(expression: e); }

`Expression`
Nonterminal =>
  i = RuleName !(ProductionRuleArguments? '=>' S)
  $ = { $$ = NonterminalExpression(name: i); }

`Expression`
Repeater =>
  '@while' S
  '(' S '*' S ')' S
  '{' S
  e = Expression
  '}' S
  $ = { $$ = ZeroOrMoreExpression(expression: e); }
  ----
  '@while' S
  '(' S '+' S ')' S
  '{' S
  e = Expression
  '}' S
  $ = { $$ = OneOrMoreExpression(expression: e); }

Type('type') =>
  '`'
  $ = <
    @while (*) {
      ! [`] [a-zA-Z0-9_$<(\{,:\})>? ]
    }
  >
  '`' S

`String`
String('string') =>
  DQString
  ----
  SQString

`String`
DQString =>
  '"'
  n = @while (*) {
    ! ["]
    $ = DQChar
  }
  '"' S
  $ = { $$ = String.fromCharCodes(n); }

`int`
DQChar =>
  ! "\\"
  $ = [{20-21}{23-5B}{5D-10FFFF}]
  ----
  "\\"
  $ = (
    EscapedValue
    ----
    EscapedHexValue
  )

`String`
SQString =>
  '\''
  n = @while (*) {
    ! [']
    $ = SQChar
  }
  '\'' S
  $ = { $$ = String.fromCharCodes(n); }

`int`
SQChar =>
  ! "\\"
  $ = [{20-26}{28-5B}{5D-10FFFF}]
  ----
  "\\"
  $ = (
    EscapedValue
    ----
    EscapedHexValue
  )

`int`
EscapedValue =>
  $ = [abefnrtv'"\\]
  {
    $ = switch ($) {
      97 => 0x07,  // a
      98 => 0x08,  // b
      101 => 0x1B, // e
      102 => 0x0C, // f
      110 => 0x0A, // n
      114 => 0x0D, // r
      116 => 0x09, // t
      118 => 0x0B, // v
      _ => $,
    };
  }
  ~ { state.error('Unexpected escape character'); }

`int`
EscapedHexValue =>
  "u"
  '{'
  $ = HexValue
  '}'
  ~ { state.malformed('Malformed escape sequence'); }

`int`
RangeChar =>
  ! "\\"
  $ = [^{0-1F}\{\}\[\]\\]
  ---
  "\\"
  $ = (
    "u"
    '{'
    $ = HexValue
    '}'
    ----
    $ = [-abefnrtv\{\}\[\]\\]
    {
      $ = switch ($) {
        97 => 0x07,  // a
        98 => 0x08,  // b
        101 => 0x1B, // e
        102 => 0x0C, // f
        110 => 0x0A, // n
        114 => 0x0D, // r
        116 => 0x09, // t
        118 => 0x0B, // v
        _ => $,
      };
    }
  )

`int`
HexValue('hex number') =>
  n = <[a-fA-F0-9]+>
  $ = { $$ = int.parse(n, radix: 16); }

`(int, int)`
Range('range') =>
  "{"
  s = HexValue
  "-"
  e = HexValue
  '}'
  $ = { $$ = (s, e); }
  ----
  "{"
  n = HexValue
  '}'
  $ = { $$ = (n, n); }
  ---
  s = RangeChar
  "-"
  e = RangeChar
  $ = { $$ = (s, e); }
  ----
  n = RangeChar
  $ = { $$ = (n, n); }
;

Block =>
  '{'
  $ = <BlockBody*>
  '}' S

`void`
BlockBody =>
  "{"
  BlockBody*
  '}'
  ----
  ! "}" .

Identifier('identifier') =>
  $ = <[a-zA-Z] [a-zA-Z0-9_]*>
  S

RuleName('production rule name') =>
  $ = <[A-Z] [a-zA-Z0-9_]*>
  S
;

S => @while (*) {
  Space
  ----
  Comment
}
;

`void`
Comment =>
  "#"
  @while (*) {
    ! EndOfLine
    .
  }
  EndOfLine?
;

`void`
Space =>
  [ \t]
  ----
  EndOfLine
;

`void`
EndOfLine =>
  "\r\n"
  ----
  [\n\r]
;
